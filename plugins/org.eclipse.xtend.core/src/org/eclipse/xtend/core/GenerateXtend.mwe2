/*******************************************************************************
 * Copyright (c) 2010 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.eclipse.xtend.core.GenerateXtend

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*
import org.eclipse.xtext.generator.parser.antlr.*
import org.eclipse.xtext.resource.XtextResourceSet 

var rootPath = ".."
var baseName = "org.eclipse.xtend.core"
var projectPath = "${rootPath}/${baseName}"

var grammarURI = "classpath:/org/eclipse/xtend/core/Xtend.xtext"
var file.extensions = "xtend"

var lineDelimiter = "\n"
var uiProjectName = "org.eclipse.xtend.ide"
var uiProjectPath = "../${uiProjectName}"
var ideProjectName = "org.eclipse.xtend.ide.common"
var ideProjectPath = "../${ideProjectName}"
var backtrack = false
var memoize = false
var skipUnusedRules = true
var stripAllComments = true

var ideaProjectName = "org.eclipse.xtend.idea"
var ideaProjectPath = "../../intellij/${ideaProjectName}"

var fileHeader = "/*******************************************************************************
 * Copyright (c) 2010-\${year} itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/"

Workflow {
	bean = StandaloneSetup {
		ignoreBrokenProjectFiles = true
		scanClassPath = true
		registerGenModelFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.genmodel"
	}
	
	bean = StandaloneSetup {
		resourceSet = XtextResourceSet : resourceSet {}
		registerEcoreFile = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.ecore"
		registerEcoreFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xtype.ecore"
		registerEcoreFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.ecore"
		registerEcoreFile = "platform:/resource/org.eclipse.xtext.xbase/model/XAnnotations.ecore"
		registerEcoreFile = "platform:/resource/${baseName}/model/Xtend.ecore"
	}
	
	component = DirectoryCleaner {
		directory = "${projectPath}/emf-gen"
	}

	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator {
		genModel = "platform:/resource/${baseName}/model/Xtend.genmodel"
		srcPath ="platform:/resource/${baseName}/src"
		srcPath ="platform:/resource/org.eclipse.xtext.common.types/src"
		srcPath ="platform:/resource/org.eclipse.xtext.xbase/src"
		lineDelimiter = lineDelimiter
	}
	
	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator {
		genModel = "platform:/resource/${baseName}/model/RichStrings.genmodel"
		srcPath ="platform:/resource/${baseName}/src"
		srcPath ="platform:/resource/org.eclipse.xtext.common.types/src"
		srcPath ="platform:/resource/org.eclipse.xtext.xbase/src"
		lineDelimiter = lineDelimiter
	}

	component = DirectoryCleaner {
		directory = "${projectPath}/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${uiProjectPath}/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${ideProjectPath}/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${ideaProjectPath}/src-gen"
	}
	
	component = parser.JFlexLoader : jFlexMain {}

	component = XtextGenerator auto-inject {
		configuration =  {
			project = WizardConfig auto-inject {
				eclipseEditor = true
				eclipsePluginRoot = uiProjectPath
				genericIdeSupport = true
				genericIdeRoot = ideProjectPath
				ideaEditor = true
				ideaPluginRoot = ideaProjectPath
			}
			code = {
				encoding = "ISO-8859-1"
				lineDelimiter = lineDelimiter
				fileHeader = fileHeader
				preferXtendStubs = false
			}
		}
		language = {
			resourceSet = resourceSet
			uri = grammarURI
			fileExtensions = file.extensions
			guiceModule = parser.XtendLanguageGeneratorModule {}
			naming = {
				eclipsePluginBasePackage = uiProjectName
				genericIdeBasePackage = ideProjectName
			}
			
			fragment = grammarAccess.GrammarAccessFragment2 auto-inject {}
			fragment = adapter.FragmentAdapter {
				fragment = serializer.SerializerFragment {
					// generateDebugData = true
				}
			}
			fragment = adapter.FragmentAdapter {
				fragment = resourceFactory.ResourceFactoryFragment {
					fileExtensions = file.extensions
				}
			}
			fragment = adapter.FragmentAdapter {
				fragment = parser.ParserGenerator auto-inject {
					options = auto-inject {}
				}
			}
			fragment = adapter.FragmentAdapter {
				fragment = DebugAntlrGeneratorFragment auto-inject {
					options = auto-inject {}
				}
			}
			fragment = validation.ValidatorFragment2 {}
			fragment = formatting.Formatter2Fragment2 {}
			fragment = generator.GeneratorFragment2 {}
			fragment = scoping.ImportNamespacesScopingFragment2 {}
			fragment = builder.BuilderIntegrationFragment2 {}
			fragment = adapter.FragmentAdapter {
				fragment = quickfix.QuickfixProviderFragment {}
			}
			fragment = adapter.FragmentAdapter {
				fragment = labeling.LabelProviderFragment {
					generateStub = false
				}
			}
			fragment = adapter.FragmentAdapter {
				fragment = outline.OutlineTreeProviderFragment {}
			}
			fragment = adapter.FragmentAdapter {
				fragment = outline.QuickOutlineFragment {}
			}
			fragment = adapter.FragmentAdapter {
				fragment = compare.CompareFragment {
					fileExtensions = file.extensions
				}
			}
			fragment = adapter.FragmentAdapter {
				fragment = contentAssist.JavaBasedContentAssistFragment {}
			}
			fragment = adapter.FragmentAdapter {
				fragment = parser.ContentAssistParserGenerator auto-inject {
					removeBacktrackingGuards = true
					lookaheadThreshold = '5'
					options = auto-inject {}
					partialParsing = true
				}
			}
			fragment = adapter.FragmentAdapter {
				fragment = refactoring.RefactorElementNameFragment {
					useJdtRefactoring = true
				}
			}
			fragment = types.TypesGeneratorFragment2 {}
			fragment = xbase.XbaseGeneratorFragment2 {
				generateXtendInferrer = false
			}
			fragment = adapter.FragmentAdapter {
				fragment = templates.CodetemplatesGeneratorFragment {
					suppressRestriction = false
				}
			}
			fragment = adapter.FragmentAdapter {
				fragment = parser.JFlexGeneratorFragment auto-inject {
					patternPath = "/org/eclipse/xtend/core/parser/antlr/internal/patterns.incl"
					additionalRulePath = "/org/eclipse/xtend/core/parser/antlr/internal/rules.incl"
				}
			}
			fragment = adapter.FragmentAdapter {
				fragment = parser.JFlexGeneratorFragment auto-inject {
					ui = true
					patternPath = "/org/eclipse/xtend/core/parser/antlr/internal/patterns.incl"
					additionalRulePath = "/org/eclipse/xtend/core/parser/antlr/internal/rules.incl"
				}
			}
			fragment = org.eclipse.xtext.xtext.generator.idea.parser.antlr.XtextAntlrIDEAGeneratorFragment auto-inject {
				options = auto-inject {}
			}
			fragment = org.eclipse.xtext.xtext.generator.idea.IdeaPluginGenerator auto-inject {}
		}
	}
}
